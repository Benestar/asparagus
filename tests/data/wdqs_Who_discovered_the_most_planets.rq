PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX v: <http://www.wikidata.org/prop/statement/>
PREFIX q: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?discoverer ?name (COUNT (DISTINCT ?planet) AS ?count) WHERE {
	?ppart wdt:P279* wd:Q634 .
	?planet wdt:P31 ?ppart ;
		wdt:P61 ?discoverer .
	OPTIONAL {
		?discoverer rdfs:label ?name .
		FILTER (LANG (?name) = "en")
	}
}
GROUP BY ?discoverer ?name
ORDER BY DESC (?count)
LIMIT 10
